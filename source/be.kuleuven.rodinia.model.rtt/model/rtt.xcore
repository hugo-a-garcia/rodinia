package be.kueleuven.rodinia.model.rtt

import be.kuleuven.rodinia.dsl.datatypes.datatypes.DataType

class OrocosPackage {
	String name
	contains TaskContext[] taskContexts
	contains ConnectionPolicy [] connectionPolicies
	contains PeerGroup[] peerGroups
}

class TaskContext {
	String [1] name
	String [1] namespace
	String [1] ^type = ""
	contains Activity[1] activity
	contains InputPort [] inputPorts
	contains EventPort[] eventPorts
	contains OutputPort [] outputPorts
	contains Property[] properties
	contains Operation[] operations
}

class PeerGroup {
	String name
	refers TaskContext [1] coordinator
	refers TaskContext [*] members
}

class ConnectionPolicy
{
	String name
	refers InputPort [1] inputPort opposite inputConnectionPolicy
	refers OutputPort [1] outputPort opposite outputConnectionPolicy
	short bufferSize
	short dataSize
	boolean init
	ConnectionPolicyLockPolicy lockPolicy = "LOCK_FREE"
	boolean pull
	ConnectionPolicyTransport transport
	ConnectionPolicyType ^type = "DATA"
}

enum ConnectionPolicyType
{
	DATA
	BUFFER = 1
	CIRCULAR_BUFFER = 2
}

enum ConnectionPolicyLockPolicy
{
	UNSYNC
	LOCKED = 1
	LOCK_FREE = 2
}

enum ConnectionPolicyTransport
{	
	^default
   CORBA = 1
   MQUEUE = 2
   ROS = 3
}

interface IActivity {
	String[1] name = "Activity"
	refers TaskContext taskContext
}

class Activity extends IActivity {
	contains Slave[] slave opposite master
	Scheduler[1] scheduler = "ORO_SCHED_OTHER"
	String[1] cpuAffinity = "~0"
	float[1] period = "0"
	int[1] priority = "0"
}

class Slave extends IActivity {
	container Activity master opposite slave
}

enum Scheduler {
	ORO_SCHED_OTHER
	ORO_SCHED_RT = 1
}

class InputPort
{
	String [1] name
	refers DataType dataType
	refers ConnectionPolicy inputConnectionPolicy opposite inputPort
}

class EventPort extends InputPort {
	
}

class OutputPort
{
	String [1] name
	refers DataType dataType
	refers ConnectionPolicy outputConnectionPolicy opposite outputPort
}

class Property {
	String name
	String description
	refers DataType ^type
	String value
}

class Operation {
	String name
	refers DataType returnType
	String documentation
}