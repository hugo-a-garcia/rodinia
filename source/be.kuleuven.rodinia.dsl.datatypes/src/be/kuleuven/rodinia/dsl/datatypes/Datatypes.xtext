grammar be.kuleuven.rodinia.dsl.datatypes.Datatypes with org.eclipse.xtext.common.Terminals

generate datatypes "http://www.kuleuven.be/rodinia/dsl/datatypes/Datatypes"

TypeModel:
		(elements+=AbstractElement)*;

QualifiedName:
  ID ('.' ID)*
;

QualifiedNameWithWildcard:
  QualifiedName '.*'?
;

AbstractElement:
	DataTypeLibrary | DataType | Import;
	
DataTypeLibrary:
	'DataTypeLibrary' name=QualifiedName
	'{'
		(elements+=AbstractElement)*
	'}';

Import:
  'import' importedNamespace = QualifiedNameWithWildcard
;

DataType:
	SimpleType | ComplexType;

SimpleType:
	'SimpleType' name=ID;

ComplexType:
	'ComplexType' name=ID ('extends' superType=[ComplexType|QualifiedName])?
	'{'
		('fields' '{' fields+=Field ( "," fields+=Field)* '}' )?
	'}'
;

Field:
	(many?='many')? name=ID ':' type=[DataType|QualifiedName];	


